# Based on community/glusterfs, only here to facilitate synced updates on local machines.
# Original maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Original contributors:
#   Andrei Antoukh - niwi@niwi.be - http://www.niwi.be
#   henning mueller <henning@orgizm.net>

_p=glusterfs
pkgname=glusterfs-local
conflicts=(glusterfs)

pkgver=3.10.0
pkgrel=1

pkgdesc='Cluster filesystem capable of scaling to several petabytes.'
arch=(i686 x86_64)
url='http://www.gluster.org/'
license=(GPL2 LGPL3)

depends=(fuse python2 libxml2 libaio liburcu attr which)
optdepends=('rpcbind: NFS' 'glib2: qemu-block')

#source=($_p-$pkgver.tar.gz::https://github.com/gluster/glusterfs/archive/v$pkgver.tar.gz)
source=(https://download.gluster.org/pub/gluster/glusterfs/${pkgver%.?}/$pkgver/glusterfs-$pkgver.tar.gz)
sha256sums=(27b114f422ebe993fdb3423c65e7fc96111db9a47ddec6fa324d935f0ad67eeb)


prepare() {
	cd "$srcdir"/$_p-$pkgver
	find -type f -name '*.py' -exec sed -i \
		-e 's:env\ python:env\ python2:' \
		-e 's:/usr/bin/python:/usr/bin/python2:' {} \;
}

build() {
	cd "$srcdir"/$_p-$pkgver
	./autogen.sh
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--with-mountutildir=/usr/bin \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--mandir=/usr/share/man \
		--libexecdir=/usr/lib/$_p \
		--with-systemddir=/usr/lib/systemd/system \
		PYTHON=python2 LEXLIB=
	make
}

package() {
	cd "$srcdir"/$_p-$pkgver

	make -j1 DESTDIR="$pkgdir" install

	mkdir -p "$pkgdir"/usr/lib/tmpfiles.d
	echo "d /var/run/glusterfs 0755 root root -" >"$pkgdir"/usr/lib/tmpfiles.d/glusterfs.conf

	install -D -m 644 \
		"$srcdir"/$_p-$pkgver/{README.md,INSTALL,COPYING*} \
		"$pkgdir"/usr/share/doc/glusterfs/

	cp -rf \
		"$srcdir"/$_p-$pkgver/doc/* \
		"$pkgdir"/usr/share/doc/glusterfs/
	rm -rf "$pkgdir"/var/run
}
