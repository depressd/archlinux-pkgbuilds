pkgname=libreswan
pkgver=3.18
pkgrel=1
pkgdesc="IPsec implementation for Linux"
arch=(i686 x86_64)
url='https://libreswan.org/'
license=(GPL MPL)
depends=(systemd nss libcap-ng libevent)
makedepends=(docbook-xsl xmlto flex bison)
conflicts=(freeswan openswan strongswan ipsec-tools)
backup=(etc/ipsec.conf etc/ipsec.secrets)
source=(
	"https://download.libreswan.org/${pkgname}-${pkgver}.tar.gz"
)
sha256sums=(
	2ff61178913287567ed2736287df47e7f9a822ddcded967f3af5f03e95b5f17d
)


libreswan_opts=(
	INITSYSTEM=systemd
	USE_DNSSEC=false
	USE_SYSTEMD_WATCHDOG=true
	USE_KEYRR=false
	USE_KLIPS=false
	USE_XAUTHPAM=false
	USE_LIBCAP_NG=true
	USE_NM=false
	USE_LIBCURL=false
	USE_EXTRACRYPTO=false
	WERROR_CFLAGS=

	INC_USRLOCAL=/usr INC_MANDIR=share/man
	FINALSBINDIR=/usr/bin FINALLIBEXECDIR=/usr/lib/ipsec
)

build() {
	cd $pkgname-$pkgver

	make "${libreswan_opts[@]}" programs
}

package() {
	cd $pkgname-$pkgver

	make "${libreswan_opts[@]}" DESTDIR="$pkgdir/" install

	sed -i '1s|python|python2|' "$pkgdir"/usr/lib/ipsec/verify
	mkdir -p "${pkgdir}"/usr/lib/tmpfiles.d
	cat >"${pkgdir}"/usr/lib/tmpfiles.d/libreswan.conf <<EOF
d /run/pluto 0755 - - -
EOF
	rm -rf "${pkgdir}"/{var,etc/pam.d}
}
