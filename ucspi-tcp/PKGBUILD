_p=ucspi-tcp

pkgname=${_p}
pkgver=0.88
pkgrel=1
pkgdesc="Command-line tools for building TCP client-server applications."
arch=('i686' 'x86_64')
url="http://cr.yp.to/ucspi-tcp.html"
license=('public-domain')
groups=()
depends=()
makedepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=(!emptydirs)
install=

source=(
	"http://cr.yp.to/$pkgname/$pkgname-$pkgver.tar.gz"
	"http://www.fefe.de/ucspi/$pkgname-$pkgver-ipv6.diff19.bz2"
)

sha256sums=(
	4a0615cab74886f5b4f7e8fd32933a07b955536a3476d74ea087a3ea66a23e9c
	35952cd290d714452c840580126004cbae8db65b1632df67ac9c8fad7d1f9ace
)


build() {
	cd "$srcdir/$pkgname-$pkgver"

	patch -p1 < ../"$pkgname-$pkgver"-ipv6.diff19 || return 1

	echo "gcc $CFLAGS -include /usr/include/errno.h" > conf-cc
	echo "gcc $LDFLAGS" > conf-ld
	echo "/usr" > conf-home

	make -j1 || return 1
}


package() {
	cd "$srcdir/$pkgname-$pkgver"

	local binaries=(
		tcpserver tcprules tcprulescheck argv0 recordio
		tcpclient *@ tcpcat mconnect mconnect-io addcr
		delcr fixcrio rblsmtpd
	)

	install -D -m0755 -t "$pkgdir"/usr/bin "${binaries[@]}" || return 1
}
