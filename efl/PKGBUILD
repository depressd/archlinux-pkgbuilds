# Based on PKGBUILD from Arch extra tree.
# Original maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Enlightenment Developers <enlightenment-devel@enlightenment.org>

pkgname=efl
pkgver=1.18.4
pkgrel=1
pkgdesc="Enlightenment Foundation Libraries"
arch=(i686 x86_64)
url="http://www.enlightenment.org"
license=(BSD LGPL2.1 GPL2 custom)
depends=(
	bullet libjpeg-turbo gst-plugins-base
	luajit curl fribidi libpulse libxcomposite
	libxinerama libxrandr libxss libinput
	libxcursor libxp libwebp shared-mime-info
	libxkbcommon lz4 openjpeg avahi
	libspectre libraw librsvg )
optdepends=(
	'python2: einabench-cmp'
	'libreoffice: thumbnailing for DOC/PPT/XLS files' )
makedepends=(python)
options=(!emptydirs)
source=(
	"http://download.enlightenment.org/rel/libs/${pkgname}/$pkgname-$pkgver.tar.xz" )
sha256sums=(39ebc07e37437d6ecdeb0f645783484e28a882b38f7e619ad12c2bf9b5548025)

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's/env python$/&2/' src/scripts/eina/eina-bench-cmp
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export CFLAGS="$CFLAGS -fvisibility=hidden"

	./configure --prefix=/usr \
		--disable-static --disable-tslib --enable-fb \
		--enable-xinput22 --enable-multisense --enable-systemd \
		--enable-image-loader-webp --enable-harfbuzz \
		--enable-liblz4 --enable-drm --enable-elput --enable-egl --with-opengl=es \
		--disable-wayland --disable-ecore-wayland --disable-wayland-ivi-shell

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR=${pkgdir} install

	# install non-standard license files
	install -Dm644 "${srcdir}/${pkgname}-${pkgver}/licenses/COPYING.BSD" \
		"${pkgdir}/usr/share/licenses/${pkgname}/COPYING.BSD"
	install -Dm644 "${srcdir}/${pkgname}-${pkgver}/licenses/COPYING.SMALL" \
		"${pkgdir}/usr/share/licenses/${pkgname}/COPYING.SMALL"
}
