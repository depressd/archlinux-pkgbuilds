# Based on PKGBUILD from Arch extra tree.
# Original maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Enlightenment Developers <enlightenment-devel@enlightenment.org>>

pkgname=enlightenment
pkgver=0.21.7
pkgrel=1
pkgdesc="Enlightenment window manager"
arch=(i686 x86_64)
url="http://www.enlightenment.org"
license=(BSD)
depends=(
	efl xcb-util-keysyms hicolor-icon-theme pixman mesa
	desktop-file-utils udisks2 ttf-font bluez-libs )
optdepends=(
	'connman: network module'
	'acpid: power events on laptop lid close'
	'geoip-database: geolocation module' )
makedepends=()
provides=(notification-daemon)
backup=(
	etc/enlightenment/sysactions.conf
	etc/xdg/menus/e-applications.menu )
source=("http://download.enlightenment.org/rel/apps/${pkgname}/$pkgname-$pkgver.tar.gz")
sha256sums=(914a305d1d1d8f51f2ae0c6a6b5a69f7cb783f701ed4d5d49efb2d57c6807df6)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export CFLAGS="$CFLAGS -fvisibility=hidden"

	./configure --prefix=/usr --sysconfdir=/etc \
		--disable-wayland --disable-wayland-egl --disable-xwayland

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="$pkgdir" install

	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
